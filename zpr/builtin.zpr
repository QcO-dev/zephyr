import "std/core.zpr";
import "zpr/ast.zpr";

function add_implicit_printu_function(): Node* {
	var name = synthetic_token(TOKEN_IDENTIFIER, "printu");
	var funktion = new_node(AST_FUNCTION, name);
	funktion.v.funktion.name = name;

	funktion.v.funktion.arguments = new_vector();

	var valTok = synthetic_token(TOKEN_IDENTIFIER, "value");
	var val = new_node(AST_DEFINE_VAR, valTok);
	val.v.variable.name = valTok;
	val.v.variable.type = INT_TYPE;
	funktion.v.funktion.arguments.push(val);

	funktion.v.funktion.returnType = VOID_TYPE;

	funktion.v.funktion.hasImplicitBody = true;

	return funktion;
}

function add_implicit_syscall_function(sysName: i8*, argCount: int): Node* {
	var name = synthetic_token(TOKEN_IDENTIFIER, sysName);
	var funktion = new_node(AST_FUNCTION, name);
	funktion.v.funktion.name = name;
	funktion.v.funktion.arguments = new_vector();

	var nrTok = synthetic_token(TOKEN_IDENTIFIER, "nr");
	var nr = new_node(AST_DEFINE_VAR, nrTok);
	nr.v.variable.name = nrTok;
	nr.v.variable.type = INT_TYPE;
	
	funktion.v.funktion.arguments.push(nr);

	for(var i = 0; i < argCount; i = i + 1) {
		var argTok = synthetic_token(TOKEN_IDENTIFIER, "arg");
		var arg = new_node(AST_DEFINE_VAR, argTok);
		arg.v.variable.name = argTok;
		arg.v.variable.type = ANY_TYPE;

		funktion.v.funktion.arguments.push(arg);
	}

	funktion.v.funktion.returnType = ANY_TYPE;

	funktion.v.funktion.hasImplicitBody = true;

	return funktion;
}

function parser_builtin_functions(): Vector* {
	var functions = new_vector();

	functions.push(add_implicit_printu_function());
	functions.push(add_implicit_syscall_function("syscall0", 0));
	functions.push(add_implicit_syscall_function("syscall1", 1));
	functions.push(add_implicit_syscall_function("syscall2", 2));
	functions.push(add_implicit_syscall_function("syscall3", 3));
	functions.push(add_implicit_syscall_function("syscall4", 4));
	functions.push(add_implicit_syscall_function("syscall5", 5));

	return functions;
}

function generate_implicit_printu_impl(out: File*) {
	out.putsln("_f_printu:");
	out.putsln("		sub     rsp, 40");
	out.putsln("		mov     eax, 10");
	out.putsln("		mov     esi, 19");
	out.putsln("		mov  r10, -3689348814741910323");
	out.putsln("		mov     WORD [rsp+20], ax");
	out.putsln("		lea     r8, [rsp+19]");
	out.putsln(".L2:");
	out.putsln("		mov     rax, rdi");
	out.putsln("		movsxd  r9, esi");
	out.putsln("		sub     r8, 1");
	out.putsln("		sub     esi, 1");
	out.putsln("		mul     r10");
	out.putsln("		mov     rax, rdi");
	out.putsln("		shr     rdx, 3");
	out.putsln("		lea     rcx, [rdx+rdx*4]");
	out.putsln("		add     rcx, rcx");
	out.putsln("		sub     rax, rcx");
	out.putsln("		mov     rcx, rdi");
	out.putsln("		mov     rdi, rdx");
	out.putsln("		add     eax, 48");
	out.putsln("		mov     BYTE [r8+1], al");
	out.putsln("		cmp     rcx, 9");
	out.putsln("		ja      .L2");
	out.putsln("		mov     rdi, 1");
	out.putsln("		mov     edx, 20");
	out.putsln("		mov     rax, 1");
	out.putsln("		sub     edx, esi");
	out.putsln("		lea     rsi, [rsp+r9]");
	out.putsln("		syscall");
	out.putsln("		add     rsp, 40");
	out.putsln("		ret");
}

function generate_builtin_functions(out: File*) {
	generate_implicit_printu_impl(out);

	var ARG_REGISTERS: i8*[6] = [ "rdi", "rsi", "rdx", "rcx", "r8", "r9" ];
	var SYS_REGISTERS: i8*[6] = [ "rax", "rdi", "rsi", "rdx", "r10", "r8" ];

	for(var i = 0; i <= 5; i = i + 1) {
		out.puts("_f_syscall"); out.putd(i); out.putsln(":");

		for(var arg = 0; arg <= i; arg = arg + 1) {
			out.puts("    mov "); out.puts(SYS_REGISTERS[arg]); out.puts(", "); out.putsln(ARG_REGISTERS[arg]);
		}
		out.putsln("    syscall");
		out.putsln("    ret");
	}
}